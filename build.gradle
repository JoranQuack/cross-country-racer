plugins {
    id 'application'
    id 'java'
    id "org.openjfx.javafxplugin" version "0.1.0"
    id 'jacoco'
}

archivesBaseName = 'seng201_team019'
group = 'seng201.team019'
version = '1.0-SNAPSHOT'
mainClassName = 'seng201.team019.App'

def javaVersion = '21'
def javafxVersion = '21.0.2'

sourceCompatibility = "${javaVersion}"
targetCompatibility = "${javaVersion}"

repositories {
    mavenCentral()
    // Add Gluon repository
    maven {
        url 'https://nexus.gluonhq.com/nexus/content/repositories/releases/'
    }
}

dependencies {
    // https://mvnrepository.com/artifact/org.openjfx/javafx-controls
    implementation "org.openjfx:javafx-controls:${javafxVersion}"

    // https://mvnrepository.com/artifact/org.openjfx/javafx-fxml
    implementation "org.openjfx:javafx-fxml:${javafxVersion}"

    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-api
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'

    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'

    // https://mvnrepository.com/artifact/com.gluonhq/charm-glisten
    implementation("com.gluonhq:charm-glisten:6.2.3")

    // https://mvnrepository.com/artifact/org.kordamp.ikonli/ikonli-core
    implementation("org.kordamp.ikonli:ikonli-core:12.3.1")

    // https://mvnrepository.com/artifact/org.kordamp.ikonli/ikonli-javafx
    implementation("org.kordamp.ikonli:ikonli-javafx:12.3.1")

    // https://mvnrepository.com/artifact/org.kordamp.ikonli/ikonli-fontawesome5-pack
    implementation("org.kordamp.ikonli:ikonli-fontawesome5-pack:12.3.1")

    // help was found here https://www.baeldung.com/mockito-junit-5-extension for version and just got same version for mockito-junit-jupiter.

    // https://mvnrepository.com/artifact/org.mockito/mockito-core
    testImplementation 'org.mockito:mockito-core:5.11.0'

    // https://mvnrepository.com/artifact/org.mockito/mockito-junit-jupiter
    testImplementation 'org.mockito:mockito-junit-jupiter:5.11.0'

    // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-databind
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.16.1'
}

application {
    // Define the main class for the application.
    mainClass = "${mainClassName}"
}

javafx {
    version = "${javafxVersion}"
    modules = ['javafx.controls', 'javafx.fxml']
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
    finalizedBy jacocoTestReport
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest {
        attributes 'Main-Class': mainClassName
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

// Jacoco test coverage config
jacoco {
    toolVersion = "0.8.9"
}

jacocoTestReport {
    executionData fileTree(project.rootDir) {
        include '**/*.exec'
    }
    reports {
        html.destination file("${buildDir}/reports/tests/coverage/")
    }
}
